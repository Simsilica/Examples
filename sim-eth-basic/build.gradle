apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'

mainClassName='example.Main'

repositories {
    mavenLocal()
    jcenter()
}

ext.jmeVersion = "[3.3,)" 

project(":sim-eth-basic-assets") {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    
    buildDir = rootProject.file("build/assets")
    
    sourceSets {
        main {
            resources {
                srcDir '.'
            }
        }
    }    
}


dependencies {
 
    // Need at least basic JME
    compile "org.jmonkeyengine:jme3-core:$jmeVersion"
    compile "org.jmonkeyengine:jme3-desktop:$jmeVersion"
    compile "org.jmonkeyengine:jme3-lwjgl:$jmeVersion"
    //Had to add this to get networking to update. Might be gradle bug from this version.
    compile("org.jmonkeyengine:jme3-networking:$jmeVersion") {
        force = true;
    } 
 
    // We definitely want a UI
    compile "com.simsilica:lemur:1.13.0"
    compile "com.simsilica:lemur-proto:1.11.0"

    // And our base code for sim-etheral, SiO2, etc.
    // (many of these need to be built locally and 'gradle install'ed as of this writing)
    compile "com.simsilica:sio2:[1.0,)"
    compile "com.simsilica:sim-math:[1.0,)"
    compile "com.simsilica:sim-ethereal:[1.0,)"  

    // Standard utility stuff
    compile 'com.google.guava:guava:19.0'
    compile 'org.slf4j:slf4j-api:1.7.13'
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
    
    runtime project(':sim-eth-basic-assets')    
}

task runServer(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "example.net.server.GameServer"
    jvmArgs '-Dlog4j.configurationFile=server-log4j2.xml'
    standardInput = System.in
}

// Create a custom server start script in the distribution
task serverStartScript(type: CreateStartScripts) {
    mainClassName = "example.net.server.GameServer"
    applicationName = "server"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
    defaultJvmOpts = ['-Dlog4j.configurationFile=server-log4j2.xml']
}

/*applicationDistribution.into("bin") {
    from(serverStartScript)
    fileMode = 0755
}*/

// I think this is ultimately clearer than the above
distributions {
    main {
        contents {
            from(serverStartScript) {
                into "bin"
            }
        }
    }
}

// Either way we end up with dupes if we don't do this
distZip {
    duplicatesStrategy = 'exclude'
}

tasks.withType(JavaCompile) {
    options.compilerArgs = ["-Xlint:deprecation", "-Xlint:unchecked"]
}