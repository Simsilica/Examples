apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'

mainClassName='mygame.Main'

repositories {
	mavenLocal()
    jcenter()
}

ext.jmeVersion = "[3.1,)" 

project(":simple-jme-assets") {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    
    buildDir = rootProject.file("build/assets")
    
    sourceSets {
        main {
            resources {
                srcDir '.'
            }
        }
    }    
}


dependencies {
 
    compile "org.jmonkeyengine:jme3-core:$jmeVersion"
    compile "org.jmonkeyengine:jme3-desktop:$jmeVersion"
    compile "org.jmonkeyengine:jme3-lwjgl:$jmeVersion"
    
    runtime project(':simple-jme-assets')    
}

task createDirs {
    doLast {    
        def pkg = 'mygame'
            def dirs = [
                file("./src/main/java/$pkg"),
                file("./src/main/resources"),
                file("./assets/Interface"),
                file("./assets/MatDefs"),
                file("./assets/Materials"),
                file("./assets/Models"),
                file("./assets/Scenes"),
                file("./assets/Shaders"),
                file("./assets/Sounds"),
                file("./assets/Textures"),    
            ]
            
            dirs.each {
                if( !it.exists() ) {
                    println "Creating " + it
                    it.mkdirs()
                }
                if( it.listFiles().length == 0 ) {
                    def stub = new File(it, 'removeme.txt') 
                    println "Creating stub file to allow git checkin, file:$stub"
                    stub.text = "Remove me when there are files here."
                }
            }
        }
}

tasks.withType(JavaCompile) {
    options.compilerArgs = ["-Xlint:deprecation", "-Xlint:unchecked"]
}

